import java.text.SimpleDateFormat

plugins {
	id 'application'
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.openjfx.javafxplugin' version '0.0.13'
	id 'org.beryx.jlink' version '2.25.0'
	id 'eclipse'
	id 'idea'
}

group = 'edu.snhu'
version = '0.0.1-SNAPSHOT'

idea {
	module {
		testSourceDirs += file("$projectDir/src/integration/java")
		testResourceDirs += [file("$projectDir/src/integration/resources"), file("$projectDir/src/test/resources")]
	}
}

ext {
	buildDate=(new SimpleDateFormat("dd-MMM-yyyy HH:mm")).format(new Date())
	moduleName="edu.snhu.library"
}

application {
	mainModule="edu.snhu.library"
	mainClass="edu.snhu.library.JavaFxApplication"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

processResources {
	filesMatching('*.properties') {
		expand(project.properties)
	}
}

repositories {
	mavenCentral()
}

java {
	sourceCompatibility = '17'
	targetCompatibility = '17'
	modularity.inferModulePath = true
}

compileJava {
	inputs.property("moduleName", moduleName)
}

jar {
	inputs.property("moduleName", moduleName)
	manifest {
		attributes('Automatic-Module-Name': moduleName)
	}
}

javafx {
	version = '17.0.2'
	modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics']
}

jlink {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
}

configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}

	//	integrationCompile.extendsFrom(testCompile)
	//	integrationRuntime.extendsFrom(integrationCompile)
}

//compileTestJava {
//	exclude '*module-info*'
//}
test {
	useJUnitPlatform()

	testLogging {     // This is for logging and can be removed.
		events = ["passed", "skipped", "failed"]
	}

	doFirst {
		jvmArgs = [
				'--add-exports', 'javafx.base/com.sun.javafx.beans=net.bytebuddy',
				'--add-reads', "$moduleName=org.mockito.junit.jupiter",
				'--add-reads', "$moduleName=javafx.fxml",
		]
	}

}

//sourceSets {
//	integration {
//		java {
//			srcDirs += "$projectDir/src/integration/java"
//			compileClasspath += main.output + configurations.integrationCompile
//			runtimeClasspath += output + compileClasspath + configurations.integrationRuntime
//		}
//		resources.srcDirs += "$projectDir/src/it/java"
//	}
//}

var os
switch(System.getProperty("os.name").toLowerCase().split(' ')[0]) {
	case 'windows':
		os='win'
		break
	case 'linux':
		os='linux'
		break
	default:
		os='mac'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.data:spring-data-commons'

	implementation 'com.fasterxml.jackson.core:jackson-core:2.13.3'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.3'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'

	implementation 'org.apache.commons:commons-lang3:3.12.0'

	// JavaFX
	implementation "org.openjfx:javafx-base:17.0.2:${os}"
	implementation "org.openjfx:javafx-controls:17.0.2:${os}"
	implementation "org.openjfx:javafx-fxml:17.0.2:${os}"
	implementation "org.openjfx:javafx-graphics:17.0.2:${os}"
	implementation "org.controlsfx:controlsfx:11.1.1"

	// Embedded MongoDB
	implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testCompileOnly 'org.projectlombok:lombok:1.18.22'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'com.vaadin.external.google', module: 'android-json'
	}
}

//task integrationTest(type: Test) {
//	testClassesDirs = sourceSets.integration.output.classesDirs
//	classpath = sourceSets.integration.runtimeClasspath
//}